DEFINE
	///////////////// SETUP SCHEMA TABLES ///////////////////
	// Establish crosswalk for columns
	VAR _DataTypes = DATATABLE(
		"Explicit Type", INTEGER,
		"Type", STRING,
		{
		{2,"Text"},
		{6,"Whole Number"},
		{8,"Decimal Number"},
		{9,"Date"},
		{10,"Currency"},			
		{11,"Boolean"}	
		})
		
	// Retireve Table info
	VAR _tables = SELECTCOLUMNS(
		FILTER(
			INFO.TABLES(),
			// Exclude hidden tables
			[IsHidden] = FALSE()
		),
		"TableID",
		[ID],
		"TableName",
		[Name]
	)
	// Get columns for the table that is the RowNumber-*
	VAR _columns = FILTER(
		INFO.COLUMNS(),
		// Exclude RowNumber columns
		[Type] <> 3
	)
	// Build Column Table
	VAR _result = SELECTCOLUMNS(
		NATURALINNERJOIN(
			_columns,
			_tables
		),
		"Table",
		[TableName],
		"Column",
		[ExplicitName],
		"Description",
		[Description],
		"Column in Data Source",
		[SourceColumn],
		"Explicit Type",
		[ExplicitDataType],
		"Inferred Type",
		[InferredDataType],
		"Data Category",
		[DataCategory],
		"Column Type",
		SWITCH(
			[Type],
			1, "Data column",
			2, "Calculated column",
			[Type]
		),
		"Format String",
		[FormatString],
		"DAX formula",
		[Expression]
	)

	// Join with Data Types to get plain language types
	VAR _schema = NATURALINNERJOIN(_result,_DataTypes)

	//////////////  Model Specific //////////////////////////

	VAR _TableName = "MarvelFact"
	
	VAR _TableCheck = SELECTCOLUMNS(
	FILTER(
		_schema,
		[Table] = _TableName
	),[Column],[Type],[Data Category],[Format String])	
	
	VAR _DataTableOutput = SUMMARIZE(_TableCheck,[Column],[Data Category],[Format String],[Type],"X","{"""&[Column] & ""","""&[Type]&""","&IF(ISBLANK([Data Category]),"BLANK()",""""&[Data Category]&"""")&","&IF(ISBLANK([Format String]),"BLANK()",""""&[Format String]&"""") & "}")
	
	VAR _Results = CONCATENATEX(_DataTableOutput,[X],",")
	
	EVALUATE ROW("VAR _z=","DATATABLE(""Column"",STRING,""Type"",STRING,""Data Category"",STRING,""Format String"",STRING,{"&_Results & "})")
